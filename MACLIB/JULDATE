*********************************************************************   JUL00970
         MACRO                                                          JUL00010
&NAME JULDATE &ADRF                                                     JUL00020
.*CONVER DATE TO JULIAN
.BIN     ANOP                                                           JUL00030
         STM   0,15,&NAME.SAVE                                          JUL00040
         SR    0,0                         CLEAR WORK REGISTER          JUL00050
         SR    1,1                         CLEAR WORK REGISTER          JUL00060
&NAME.GO  DS   0H                                                       JUL00130
         PACK  &NAME.DW(2),&ADRF+6(2)       YEAR
         ICM   0,3,&NAME.DW                 YEAR                        JUL00140
         SRL   0,4                         SHIFT OUT SIGN
         STC   0,&NAME.JYR+1
         ICM   1,1,&NAME.LEAP               LAST LEAP YEAR              JUL00150
         SR    0,1                           SUBTRACT YEARS             JUL00160
         LTR   0,0                                                      JUL00170
         BZ    &NAME.SET                 LEAP YEAR                      JUL00180
         C     0,=F'4'                   IF DIFFERENCE = 4              JUL00190
         BE    &NAME.SET                 THEN THIS IS LEAP YEAR         JUL00200
         BL    &NAME.GDAY                IF , 4, THEN NOT LEAP YEAR     JUL00210
         LR    1,0
         SR    0,0
         D     0,=F'4'                   IS RESULT DIVISIBLE BY 4       JUL00220
         C     0,=F'4'                   TEST REMAINDER                 JUL00230
         BNE   *+8                        IF REMAINDER = 4, THEN LEAP   JUL00240
&NAME.SET DS   0H                          SET LEAP YEAR SWITCH         JUL00250
         OI    &NAME.LSW,X'01'            SET LEAP YEAR SWITCH          JUL00260
&NAME.GDAY DS  0H                        CALCULATE DAY                  JUL00270
         SR    0,0                      CLEAR WORK REG                  JUL00280
         LA    1,1                      INITIALIZE TO 1ST MONTH         JUL00290
         SR    4,4                      CLEAR WORK REG                  JUL00300
         CLC   &ADRF+0(2),=CL2'01'      FIRST MONTH
         BE    &NAME.DGOT
         PACK  &NAME.DW,&ADRF+0(2)    PACK MONTH VALUE
         CVB   0,&NAME.DW                   CONVERT TO BINARY           JUL00320
         LA    3,&NAME.RDAY             LOAD NUMB. OF DAYS TABLE        JUL00330
         CLI   &NAME.LSW,X'01'            IF LEAP YEAR USE LEAP         JUL00340
         BNE   *+8                      YEAR TABLE                      JUL00350
         LA    3,&NAME.LDAY            LEAP YEAR TABLE                  JUL00360
         XC    &NAME.DW(2),&NAME.DW   ZERO WORK AREA                    JUL00370
         ICM   4,1,0(3)               #DAYS IN FIRST MONTH              JUL00380
&NAME.CALC DS  0H                       BEGIN CALCULATION               JUL00400
         LA    3,1(3)      NEXT MONTHS DAYS
         LA    1,1(1)      INCREMENT COUNTER-R1 WILL POINT TO MONTH     JUL00410
         CR    0,1         STOP WHEN GIVEN MONTH EQUAL
         BE    &NAME.DGOT
         MVC   &NAME.DW+1(1),0(3)             #OF DAYS IN MONTH         JUL00420
         AH    4,&NAME.DW              ACCUMULATE DAYS SO FAR           JUL00430
         B     &NAME.CALC        REPEAT UNTIL MONTHS EQUAL              JUL00520
&NAME.DGOT DS   0H               MONTHS EQUAL - ADD IN GIVEN DAY        JUL00530
         PACK  &NAME.DW,&ADRF+3(2)    PACK DAY VALUE
         CVB   0,&NAME.DW                   CONVERT TO BINARY           JUL00320
         AR    4,0               DAY NUMBER
         CVD   4,&NAME.DW
         ZAP   &NAME.JDAY,&NAME.DW+6(2)
         B     &NAME.E                                                  JUL00820
&NAME.RDAY DC X'1F1C1F1E1F1E1F1F1E1F1E1F'  DAYS IN MONTH                JUL00830
&NAME.LDAY DC X'1F1D1F1E1F1E1F1F1E1F1E1F'  DAYS IN MONTH-LEAPYEAR       JUL00840
&NAME.LEAP DC X'80'          LAST LEAP YEAR WAS 1980                    JUL00850
&NAME.LSW  DC X'00'          SWITCH FOR LEAP YEAR                       JUL00860
&NAME.JDTE  DS 0CL4
&NAME.JYR   DS XL2
&NAME.JDAY DS PL2                                                       JUL00900
&NAME.SAVE DS 9D                                                        JUL00920
&NAME.DW   DS D                                                         JUL00930
.END     ANOP                                                           JUL00940
&NAME.E  DS  0H                                                         JUL00950
         LM   0,15,&NAME.SAVE
         MEND                                                           JUL00960
