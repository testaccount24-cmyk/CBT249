        MACRO
&NAME  CONVHEX &IN=(2),&OUT=(1),&LEN=(0),&WORK=(5)
&NAME   DS  0H   CONVERT ERROR CODES FOR DISPLAY
.*ADDR OF WORK AREA IN R5; OUTPUT AREA IN R1; INPUT AREA IN R2; LEN R0
.*LEN CAN BE REG OR ABS VALUE
        LCLC  &WR
        LCLC  &IR
        LCLC  &OR
        LCLC  &LR
.SETWORK ANOP
        AIF   ('&WORK'(1,1) EQ '(').SETWREG
&WR     SETC  '&WORK'
        AGO   .SETIN
.SETWREG ANOP
&WR     SETC  '0&WORK'   SET WORK ADDR
        AGO   .SETIN
.SETIN  ANOP
        AIF   ('&IN'(1,1) EQ '(').SETIREG
&IR     SETC  '&IN'      SET INPUT ADDR
        AGO  .SETOUT
.SETIREG  ANOP
&IR     SETC  '0&IN'     SET INPUT ADDR
        AGO  .SETOUT
.SETOUT  ANOP
        AIF   ('&OUT'(1,1) EQ '(').SETOREG
&OR     SETC  '&OUT'      SET OUTPUT ADDR
        AGO  .SETLEN
.SETOREG ANOP
&OR     SETC  '0&OUT'     SET OUTPUT ADDR
        AGO  .SETLEN
.SETLEN ANOP
        AGO  .ALLSET
.ALLSET  ANOP
&NAME.CONV DS  0H
.*CONVERT HEX CODES TO DISPLAY
     STM   14,12,&WR
         LA    3,&IR               FIELD TO CONVERT-1
         BCTR  3,0
        LA 1,&OR             ADDR TO MOVE TO - 2
        BCTR  1,0
        BCTR  1,0
        AIF   (K'&LEN LT 3).ABSLEN
        AIF   (K'&LEN GT 3).LR2
        AIF    ('&LEN'(1,1) NE '(').ABSLEN  L'FIELD
&LR     SETC  '&LEN'(2,1)
        LR    2,&LR             LEN OF FIELD
        AGO   .GOCONV
.LR2     ANOP
        AIF    ('&LEN'(1,1) NE '(').ABSLEN  L'FIELD
&LR      SETC  '&LEN'(2,2)
         LR   2,&LR             LEN OF FIELD
         AGO  .GOCONV
.ABSLEN  ANOP     LEN ABS VALUE  LEN OF FIELD
         LA    2,&LEN            LEN OF FIELD
         AGO  .GOCONV
.GOCONV  ANOP
******
&NAME.GO DS    0H
         LA    1,2(1)              ADDR TO MOVE TO
         LA    3,1(3)              FIELD TO CONVERT
         IC    0,0(3)              LOAD CHAR
         SRL   0,4                 SHIFT OVER 4 BITS
         STC   0,&NAME.CHAR        STORE IT
         NI   &NAME.CHAR,X'0F'    ZERO OUT HI 4 BITS
         TR  &NAME.CHAR,&NAME.TAB  TRANSLATE TO DEC
         MVC   0(1,1),&NAME.CHAR  MOVE TO TD ...
         MVC &NAME.CHAR,0(3)      MOVE FIELD
         NI  &NAME.CHAR,X'0F'     ZERO OUT HI 4 BITS
         TR  &NAME.CHAR,&NAME.TAB  TRANSLATE TO DEC
         MVC   1(1,1),&NAME.CHAR  MOVE TO TD ...MSGRET
         BCT   2,&NAME.GO         NEXT BYTE IF ANY
      LM   14,12,&WR
       B    &NAME.END
&NAME.TAB DC   X'F0F1F2F3F4F5F6F7F8F9C1C2C3C4C5C6'
&NAME.CHAR DS  X
&NAME.END DS 0H
       MEND
