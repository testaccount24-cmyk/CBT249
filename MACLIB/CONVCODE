          MACRO
&NAME  CONVCODE &IN=0(3),&LEN=0(2),&OUT=0(1)
&NAME.CONV DS  0H                  CONVERT TO DISPLAY
         STM   1,3,&NAME.SAVE
         LA    1,&OUT            ADDR TO MOVE TO
         LA    3,&IN             FIELD TO CONVERT
         LA    2,&LEN
&NAME.GO  DS   0H
         IC    0,0(3)            LOAD CHAR
         SRL   0,4                 SHIFT OVER 4 BITS
         STC   0,&NAME.CHAR        STORE IT
         NI  &NAME.CHAR,X'0F'     ZERO OUT HI 4 BITS
         TR   &NAME.CHAR,&NAME.TAB TRANSLATE TO DEC
         MVC   0(1,1),&NAME.CHAR  MOVE TO OUTPUT
         MVC  &NAME.CHAR,0(3)     MOVE FIELD
         NI   &NAME.CHAR,X'0F'    ZERO OUT HI 4 BITS
         TR  &NAME.CHAR,&NAME.TAB
         MVC   1(1,1),&NAME.CHAR  MOVE TO TD ...MSGRET
         LA    3,1(3)
         LA    1,2(1)
         BCT   2,&NAME.GO         NEXT BYTE IF ANY
          LM   1,3,&NAME.SAVE
           B    &NAME.END
&NAME.SAVE DS  3F
&NAME.TAB DC   X'F0F1F2F3F4F5F6F7F8F9C1C2C3C4C5C6'
&NAME.CHAR DS  C
&NAME.END  DS   0H
          MEND
