       MACRO
&NAME CONVDAY &ADRF                                                     JUL00020
.*CONVERT JULIAN DATE TO CALENDAR DATE
.BIN     ANOP                                                           JUL00030
         STM   0,15,&NAME.SAVE                                          JUL00040
         SR    0,0                         CLEAR WORK REGISTER          JUL00050
         SR    1,1                         CLEAR WORK REGISTER          JUL00060
         CP    &ADRF+2(2),=P'32'                                        JUL00070
         BNL   &NAME.GO                   NOT FIRST MONTH               JUL00080
         UNPK  &NAME.DD,&ADRF+2(2)                                      JUL00090
         OI    &NAME.DD+1,X'F0'                                         JUL00100
         MVC   &NAME.MM,=CL2'01'                                        JUL00110
         B     &NAME.GYR                                                JUL00120
&NAME.GO  DS   0H                                                       JUL00130
         MVO  &NAME.DW+6(2),&ADRF+1(1)
         OI   &NAME.DW+7,X'0F'
         CVB   0,&NAME.DW
         XC    &NAME.DW,&NAME.DW      FIX FOR LEAP YR
         MVC  &NAME.DW+6(1),&ADRF+1
         MVI   &NAME.DW+7,X'0C'     FIX FOR LEAP YR
         MVO   &NAME.DW+6(2),&NAME.DW+6(1) FIX FOR LEAP YR
         ICM   1,1,&NAME.LEAP               LAST LEAP YEAR              JUL00150
         SR    0,1                           SUBTRACT YEARS             JUL00160
         LTR   0,0                                                      JUL00170
         BZ    &NAME.SET                 LEAP YEAR                      JUL00180
         C     0,=F'4'                   IF DIFFERENCE = 4              JUL00190
         BE    &NAME.SET                 THEN THIS IS LEAP YEAR         JUL00200
         BL    &NAME.GDAY                IF , 4, THEN NOT LEAP YEAR     JUL00210
         LR    1,0
         SR    0,0
         D     0,=F'4'                   IS RESULT DIVISIBLE BY 4       JUL00220
         C     0,=F'0'                   TEST REMAINDER                 JUL00230
         BNE   *+8                        IF REMAINDER = 4, THEN LEAP   JUL00240
&NAME.SET DS   0H                          SET LEAP YEAR SWITCH         JUL00250
         OI    &NAME.LSW,X'01'            SET LEAP YEAR SWITCH          JUL00260
&NAME.GDAY DS  0H                        CALCULATE DAY                  JUL00270
         SR    0,0                      CLEAR WORK REG                  JUL00280
         LA    1,1                      INITIALIZE TO 1ST MONTH         JUL00290
         SR    4,4                      CLEAR WORK REG                  JUL00300
         ZAP   &NAME.DW,&ADRF+2(2)       ZAP DAY VALUE                  JUL00310
         CVB   0,&NAME.DW                   CONVERT TO BINARY           JUL00320
         LA    3,&NAME.RDAY             LOAD NUMB. OF DAYS TABLE        JUL00330
         CLI   &NAME.LSW,X'01'            IF LEAP YEAR USE LEAP         JUL00340
         BNE   *+8                      YEAR TABLE                      JUL00350
         LA    3,&NAME.LDAY            LEAP YEAR TABLE                  JUL00360
         XC    &NAME.DW(2),&NAME.DW   ZERO WORK AREA                    JUL00370
         ICM   4,1,0(3)               #DAYS IN FIRST MONTH              JUL00380
         LA    3,1(3)                  POINT TO SECOND MONTH            JUL00390
&NAME.CALC DS  0H                       BEGIN CALCULATION               JUL00400
         LA    1,1(1)      INCREMENT COUNTER-R1 WILL POINT TO MONTH     JUL00410
         MVC   &NAME.DW+1(1),0(3)             #OF DAYS IN MONTH         JUL00420
         AH    4,&NAME.DW              ACCUMULATE DAYS SO FAR           JUL00430
         LA    4,1(4)                                                   JUL00440
         CR    0,4         IF CUM DAYS+1=JUL DAY, THEN FIRST IN MO      JUL00450
         BE    &NAME.FIR        FIRST DAY IN MONTH                      JUL00460
         BCTR  4,0              RESET CUM DAYS                          JUL00470
         CR    0,4              JUL DAY:CUM DAYS                        JUL00480
         BE    &NAME.VALG         IF =, THEN LAST DAY IN MONTH          JUL00490
         BL    &NAME.VSUB         IF <, THEN SUB LAST MONTHS DAYS       JUL00500
         LA    3,1(3)      POINT TO NEXT MONTHS DAYS                    JUL00510
         B     &NAME.CALC        REPEAT UNTIL JUL DAY NOT HIGHER        JUL00520
&NAME.FIR  DS   0H               DAY = FIRST IN MONTH                   JUL00530
         LA    1,1(1)             INCREMENT MONTH                       JUL00540
         LA    0,1                SET DAY TO ONE                        JUL00550
         B     &NAME.DONE                                               JUL00560
&NAME.VALG DS   0H               DAY = LAST IN MONTH                    JUL00570
******   LA    3,1(3)            POINT TO NEXT MONTHS DAYS              JUL00580
         SR     0,0                                                     JUL00590
         ICM    0,1,0(3)         VALUE=#DAYS IN MONTH                   JUL00600
         B      &NAME.DONE                                              JUL00610
&NAME.VSUB DS   0H                 SUBTRACT VALUE FROM CUM DAYS         JUL00620
         MVC   &NAME.DW+1(1),0(3)   #DAYS IN CURRENT MONTH              JUL00630
           SH   4,&NAME.DW          SUBTRACT DAYS FROM CUM DAYS         JUL00640
           SR   0,4                 JUL DAY-CUM DAYS=DAY VAL            JUL00650
           B    &NAME.DONE                                              JUL00660
&NAME.DONE DS   0H                 FORMAT CALCULATED DATE               JUL00670
**AT THIS  POINT R0 CONTAINS THE DAY; R1 CONTAINS THE MONTH (-1)        JUL00680
           STH  0,&NAME.BIMO      SAVE DAY IN BINARY                    JUL00690
           STH  1,&NAME.BMON      SAVE DAY IN BINARY                    JUL00690
           CVD  1,&NAME.DW        CONVERT MONTH TO DECIMAL              JUL00700
           OI   &NAME.DW+7,X'0F'   STRIP SIGN                           JUL00710
           UNPK &NAME.MM,&NAME.DW+6(2) UNPACK MONTH                     JUL00720
           CVD  0,&NAME.DW        CONVERT DAY   TO DECIMAL              JUL00730
           OI   &NAME.DW+7,X'0F'   STRIP SIGN                           JUL00740
           UNPK &NAME.DD,&NAME.DW+6(2) UNPACK DAY                       JUL00750
&NAME.GYR  DS   0H                                                      JUL00760
           MVI  &ADRF+2,X'0C'      PACK YEAR                            JUL00770
           SRP  &ADRF.(3),63,0    SHIFT HALF BYTE TO RIGHT              JUL00780
           OI   &ADRF+2,X'0F'      STRIP SIGN                           JUL00790
           UNPK &NAME.YY,&ADRF+1(2)  UNPACK YEAR AND STORE              JUL00800
         LM    0,15,&NAME.SAVE                                          JUL00810
         B     &NAME.E                                                  JUL00820
&NAME.RDAY DC X'1F1C1F1E1F1E1F1F1E1F1E1F'  DAYS IN MONTH                JUL00830
&NAME.LDAY DC X'1F1D1F1E1F1E1F1F1E1F1E1F'  DAYS IN MONTH-LEAPYEAR       JUL00840
.*&NAME.LEAP DC X'50'          LAST LEAP YEAR WAS 1980                  JUL00850
&NAME.LEAP DC X'00'          LAST LEAP YEAR WAS 2000                    JUL00850
&NAME.LSW  DC X'00'          SWITCH FOR LEAP YEAR                       JUL00860
&NAME.MM   DS CL2                                                       JUL00870
&NAME.DD   DS CL2                                                       JUL00880
&NAME.YY   DS CL2                                                       JUL00890
&NAME.JDAY DS PL2                                                       JUL00900
&NAME.BIMO DS H                                                         JUL00910
&NAME.BMON DS H                                                         JUL00910
&NAME.SAVE DS 9D                                                        JUL00920
&NAME.DW   DS D                                                         JUL00930
.END     ANOP                                                           JUL00940
&NAME.E  DS  0H                                                         JUL00950
         MEND                                                           JUL00960
